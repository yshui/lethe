import "o.idx" as T;
vertex ballv(
	vec2 position,
	vec2 translate,
	vec2 texture_coord,
	float angle,
	float alpha
);
vertex hitball(
	float r,
	vec2 center
);
vertex hittri (
	vec2 p1, vec2 p2, vec2 p3
);
event Collide(particle);
event nextFrame(float);
add1(int a) -> int {
	=> a+1;
}
particle Wall {
	vec2 start;
	vec2 end;
	state Update @ nextFrame {
		hitboxes << `hittri(start, end, (start+end)/2.0);
	};
}
particle WallN << Wall{
	>(float w) {
		start <- vec(0, 10);
		end <- vec(w, 10);
		`Update;
	}
}
particle WallS << Wall{
	>(float w, float h) {
		start <- vec(0, h-10.0);
		end <- vec(w, h-10.0);
		`Update;
	}
}
particle WallW << Wall{
	>(float h) {
		start <- vec(10, 0);
		end <- vec(10, h);
		`Update;
	}
}
particle WallE << Wall{
	>(float w, float h) {
		start <- vec(w-10.0, 0);
		end <- vec(w-10.0, h);
		`Update;
	}
}
particle Spark {
	vec2 center;
	float r;
	vec2 velocity;
	float alpha;
	>(center, float v) {
		r <- 20.0;
		angle = rand(0..6.28);
		velocity <- vec(v*cos(angle), v*sin(angle));
		alpha<- 1.0;
		`Decay;
	}
	state Decay @ nextFrame {
		center <- center+velocity;
		r <- r*0.9;
		velocity <- velocity*0.94;
		alpha <- alpha*0.9;
		render << `ballv(vec(-1, 1)*r, center, T.Star.bl, 0, alpha),
			  `ballv(vec(-1, -1)*r, center, T.Star.ul, 0, alpha),
			  `ballv(vec(1, -1)*r, center, T.Star.ur, 0, alpha),
			  `ballv(vec(1, 1)*r, center, T.Star.br, 0, alpha);
		render << -4, -3, -2, -4, -2, -1;
		if (r < 1e-6)
			=> Deleted;
	};
}
particle Ball {
	vec2 center;
	vec2 velocity;
	float r;
	state Update @ nextFrame {
		center <- center+velocity;
		render << `ballv(vec(-1, 1)*r, center, T.Ball.bl, 0,1),
			  `ballv(vec(-1, -1)*r, center, T.Ball.ul, 0,1),
			  `ballv(vec(1, -1)*r, center, T.Ball.ur, 0,1),
			  `ballv(vec(1, 1)*r, center, T.Ball.br, 0,1);
		render << -4, -3, -2, -4, -2, -1;
		hitboxes << `hitball(r, center);
	};
	state Move @ Collide(Ball(x)) {
		line = x.center - center;
		dist = line *: line;
		vm = line *. velocity / dist;
		vo = line *. x.velocity / dist;
		if (vo-vm <= 0.0) {
			tmpv = velocity-vm*line/dist;
			velocity <- tmpv+vo*line/dist;
			poc = center+line/2.0;
			loop(0..5)
				`Spark(poc,(vm-vo)*2.0);
		}
	},
	@ Collide(WallN(x)) {
		if (velocity.y < 0.0)
			velocity <- vec(velocity.x, -velocity.y);
	},
	@ Collide(WallS(x)) {
		if (velocity.y > 0.0)
			velocity <- vec(velocity.x, -velocity.y);
	},
	@ Collide(WallW(x)) {
		if (velocity.x < 0.0)
			velocity <- vec(-velocity.x, velocity.y);
		nextState = Move;
	},
	@ Collide(WallE(x)) {
		if (velocity.x > 0.0)
			velocity <- vec(-velocity.x, velocity.y);
	};
	>(center, velocity, r) {
		`Update;
		`Move;
	}
}
particle Bootstrap {
	>(float w, float h) {
		loop(i~0..150)
			`Ball(rand(vec(10, 10)..vec(w, h)),
			      rand(vec(-2, -2)..vec(2, 2)),
			      rand(4..9));
		`WallN(w);
		`WallW(h);
		`WallE(w, h);
		`WallS(w, h);
	}
}
